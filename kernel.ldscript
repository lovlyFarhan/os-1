/* Script for -z combreloc: combine and sort reloc sections */
OUTPUT_FORMAT("elf32-littlearm", "elf32-bigarm",
	      "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(_start)
SEARCH_DIR("/opt/local/arm-none-eabi/lib");
SECTIONS
{
    __KernelStart = 2 * 1024 * 1024 * 1024;
    __PhysicalRamSize = 128 * 1024 * 1024;

    . = 0x0;

    .early.text :
        {
        kernel/early-*.o(.text.early.vector)
        kernel/early-*.o(.text)
        }

    .early.data :
        {
        kernel/early-*.o(.data .data.*)
        kernel/early-*.o(.bss .bss.*)
        }

    . = ALIGN(CONSTANT(MAXPAGESIZE));
    begin_high = .;

    .text (begin_high + __KernelStart) : AT(begin_high)
        ALIGN(CONSTANT(MAXPAGESIZE))
        {
            *(.text)
        }

    .data :
        ALIGN(CONSTANT(MAXPAGESIZE))
        {
            *(.data)
            *(.rodata)
        }

    .init_array :
        {
        PROVIDE_HIDDEN(__init_array_start = .);
        KEEP (*(SORT(.init_array.*)))
        KEEP (*(.init_array))
        PROVIDE_HIDDEN (__init_array_end = .);
        }

    .bss :
        ALIGN(CONSTANT(MAXPAGESIZE))
        {
            *(.bss)
        }

    .ARM.extab :
        {
        *(.ARM.extab* .gnu.linkonce.armextab.*)
        }

    PROVIDE_HIDDEN(__exidx_start = .);
    .ARM.exidx :
        {
        *(.ARM.exidx* .gnu.linkonce.armexidx.*)
        }
    PROVIDE_HIDDEN(__exidx_end = .);

    PROVIDE (end = .);

    /* Hop forward in virtual memory to the ARM high vector table address */
    .vector 0xffff0000 : AT(ALIGN(LOADADDR(.ARM.exidx) + SIZEOF(.ARM.exidx), CONSTANT(COMMONPAGESIZE)))
        {
            __VectorStartPhysical = LOADADDR(.vector);
            *(.text.vector)
        }

    /* Reset VMA counter as though .vector had been in normal kernel range */
    . = ADDR(.ARM.exidx)
            + (LOADADDR(.vector) - LOADADDR(.ARM.exidx))  /* Padded sizeof(.ARM.exidx) */
            + SIZEOF(.vector);

    __HeapStart = ALIGN(CONSTANT(MAXPAGESIZE));
    __RamEnd = __KernelStart + __PhysicalRamSize;
}
